//------------INTERRUPT 6 MODULE---------

//-----------READ SYSTEM CALL-----------
//change mode_flag of curr_proc to 7(read sys_call)
[PROCESS_TABLE+([SYSTEM_STATUS_TABLE+1]*16)+9]=7;

alias userSP R0;
userSP = SP;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 13] = SP;
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11] * 512 - 1;

alias physical_page R1;
alias offset R2;
alias file_descriptor R3;

//getting file descriptor
physical_page = [PTBR + 2 * ((userSP - 4) / 512)];
offset = (userSP - 4) % 512;
file_descriptor = [physical_page * 512 + offset];
if(file_descriptor != -1) then
	[[PTBR + 2 * ((userSP - 1) / 512)] * 512 + ((userSP - 1) % 512)] = -1;
else
	alias content_addr R4;
	physical_page = [PTBR + 2 * ((userSP - 3) / 512)];
	offset = (userSP - 3) % 512;
	content_addr = [physical_page * 512 + offset];

	backup;	
	R1 = 4;				//TERMINAL READ = 4
	R2 = [SYSTEM_STATUS_TABLE + 1];	//curr_proc pid
	R3 = content_addr;		//address of word
	call MOD_4;			//device manager module
	restore;

	[[PTBR + 2 * ((userSP - 1) / 512)] * 512 + ((userSP - 1) % 512)] = 0;	//success
endif;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;	//user mode

SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 13];

ireturn;

//--------------------------------------

//---------------------------------------
